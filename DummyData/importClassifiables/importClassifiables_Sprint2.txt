// ******* Before Using! ******* //
// WHAT THE SCRIPT WILL DO:
// Will update the database constraints.
// Will delete ALL your old data
// Load Classifiables, their Concept Strings and connect them
//
// WHAT YOU (may) NEED TO DO:
// Make sure the filepaths match to where your file actually is
// Right now they're defaulted using what we have on our repo. So if that 
// does not work, then you will need to the path so you load it from your
// machine.
// Locally: slashes and case sensitivity for drives must match 
// examples below

// Drop Old constraint.
// NOTE: If you start a new database, then you can skip this step.
// NOTE: If it says it can't find it, then you're good! Yay!
DROP CONSTRAINT ON (c:Classifiable) ASSERT c.name is UNIQUE;

// Add new id constraint to Classifiables
// NOTE: If it says it returned 0 rows, then you don't have to run
// this command again in your current DB. Yay!
CREATE CONSTRAINT ON (c:Classifiable) ASSERT c.id IS UNIQUE;

// Delete the Classifiables and ANY relationship they had with anything else
MATCH (n:Classifiable)
OPTIONAL MATCH (n)-[r]-()
DELETE n,r;

// Load Classifiables
USING PERIODIC COMMIT 1000
LOAD CSV WITH HEADERS FROM 
"https://raw.githubusercontent.com/BasicConceptClassification/ClassificationDeveloper/master/DummyData/importClassifiables/classifiables_hilights_combined.csv"
AS line
MERGE (c:Classifiable {id:line.ItemId})
ON CREATE SET c.id=line.ItemId, c.name = line.ItemName, c.url=line.Glam;

// Load Concept Strings and link them to the Classifiables 
// Right now, if there is no concept string, it gets added as
// "" and will be connected to that
// While not ideal, it does make all the non-classifieds easy
// to find
USING PERIODIC COMMIT 1000
LOAD CSV WITH HEADERS FROM 
"https://raw.githubusercontent.com/BasicConceptClassification/ClassificationDeveloper/master/DummyData/importClassifiables/classifiables_hilights_combined.csv"
AS line
MATCH (c:Classifiable {id:line.ItemId})
MERGE (cs:ConceptString {terms: TRIM(line.ConceptString) })
CREATE (c)-[:HAS_CONSTR]->(cs);