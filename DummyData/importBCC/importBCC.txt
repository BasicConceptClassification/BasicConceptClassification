// Importing the Classification/Controlled Vocabulary
// Thanks to: 
// http://jexp.de/blog/2014/06/load-csv-into-neo4j-quickly-and-successfully/
// for help with loading, finding the path and placing constraints

// ******* Before Using! ******* //
// WHAT THE SCRIPT WILL DO:
// Will update the database constraints.
// Load Terms and connect them
//
// WHAT YOU (may) NEED TO DO:
// Make sure the filepaths match to where your file actually is
// Right now they're defaulted using what we have on our repo. So if that 
// does not work, then you will need to the path so you load it from your
// machine.
// Locally: slashes and case sensitivity for drives should be like the following 
// example:
// "file:c:/Users/Bronte/Source/Repos/ClassyDev/DummyData/importBCC/BCC_Sprint2.csv"
//
// Current issue: There are some terms with multiple parents. They are currently
// connected to just the first one that was listed.

// STEP 0: If you've added the old terms before, do this step
// Otherwise, you can skip it. If it FAILS, it's OKAY.
DROP CONSTRAINT ON (t:Term) ASSERT t.name IS UNIQUE;

// STEP 0.1: If each Term has a unique Id
// If it FAILS, it's FINE. You won't have to run this step again.
CREATE CONSTRAINT ON (t:Term) ASSERT t.id IS UNIQUE;

// STEP 1: Delete Previous Terms.
// WIll take some time if you have already connected the terms
MATCH (n:Term)
OPTIONAL MATCH (n)-[r]-()
DELETE n,r

// STEP 2: This will take a little more time than usual. Creates about 2500 nodes!
// Creates almost all the terms. 
// LOWER to make merging by name easier for classifiables
USING PERIODIC COMMIT 1000
LOAD CSV WITH HEADERS FROM 
"https://raw.githubusercontent.com/BasicConceptClassification/ClassificationDeveloper/master/DummyData/importBCC/BCC_Sprint2.csv"
AS line
MERGE (term: Term {id:line.TermId})
ON CREATE SET term.rawTerm = line.TermLabel, term.id = line.TermId;

// STEP 3: THIS WILL TAKE SOME TIME! Depending on your computer, 
// might take a few minutes 
// Connects the parent and children nodes.
USING PERIODIC COMMIT 1000
LOAD CSV WITH HEADERS FROM 
"https://raw.githubusercontent.com/BasicConceptClassification/ClassificationDeveloper/master/DummyData/importBCC/BCC_Sprint2.csv"
AS line
MATCH (p {id:line.TermSubProperty})
MATCH (c {id:line.TermId})
CREATE (c)-[:SUBTERM_OF]->(p);

// STEP 3.5: add a lower property to make merging easier when connecting to concept string terms.
// not sure why this needs to be a separate step?
// when combined with step 2, it makes step 3 get an SSL disconnect issue. Might be because of accessing github for the file?
USING PERIODIC COMMIT 1000
LOAD CSV WITH HEADERS FROM 
"https://raw.githubusercontent.com/BasicConceptClassification/ClassificationDeveloper/master/DummyData/importBCC/BCC_Sprint2.csv"
AS line
MATCH (term: Term {id:line.TermId})
SET term.lower = LOWER(line.TermLabel);

// Step 4: Create the actual root of the BCC
CREATE (t:BccRoot {rawTerm:"BccRoot", id:"bccRoot", lower:"bccroot"});

// Step 5: Then connect all those Terms without a parent to the BccRoot
MATCH (t:Term), (top:BccRoot {id:"bccRoot"})
WHERE NOT (t)-[:SUBTERM_OF]->()
MERGE (t)-[:SUBTERM_OF]->(top);
