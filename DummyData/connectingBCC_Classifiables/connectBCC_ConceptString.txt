// ******* Connecting Classifiable's Concept String with the Terms! ******* //
// You must have done the following:
//    - imported BCC (after commit 2236f16e77eb06f6a125bf9b669290aa4c9c442e, Feb 21)
//    - imported Classifiables (see note below)

// If you ever need to delete the dummy terms (they'll have no id), use this:
MATCH (t:Term)-[r:HAS_TERM]-()
WHERE NOT HAS (t.id)
DELETE t, r;

// STEP 0:
// EIther import the fixed Classifiables .csv file OR run this fix:
USING PERIODIC COMMIT 1000
LOAD CSV WITH HEADERS FROM 
"https://raw.githubusercontent.com/BasicConceptClassification/ClassificationDeveloper/master/DummyData/importClassifiables/classifiables_hilights_combined.csv"
AS line
MATCH (c:Classifiable {id:line.ItemId})-[:HAS_CONSTR]->(cs)
SET cs.terms = TRIM(line.ConceptString);

// STEP 1: Connect all the classifiable's concept strings that are not null ("")
// to terms that already exist or create new "dummy" terms which are not in the
// Classification properly yet
MATCH (c:Classifiable)-[:`HAS_CONSTR`]->(cs:ConceptString)
WITH c, cs, REPLACE(cs.terms, "(", "") AS trmStr
UNWIND ( FILTER
			( x in 
				SPLIT( LEFT(trmStr, LENGTH(trmStr)), ")" ) 
				WHERE x <> ""
		  	) 
		) AS t4
MERGE (t:Term{lower:LOWER(t4)})
ON CREATE SET t.rawTerm=t4, t.lower=Lower(t4)
MERGE (cs)-[:HAS_TERM]->(t);

// Step 2: Create a parent for the dummy terms:
CREATE (t:Term {rawTerm:"Not a Real Term", id:"notRealId", lower:"not a real term"});

// Step 3: Connect the dummy terms to the parent:
MATCH (t:Term), (t2:Term{id:"notRealId"})
WHERE NOT HAS (t.id)
MERGE (t)-[:SUBTERM_OF]->(t2);

// Step 4: Create the actual root of the BCC
// If you did this in import BCC, don't have to do it again.
CREATE (t:BccRoot {rawTerm:"BccRoot", id:"bccRoot", lower:"bccroot"});

// Step 5: Then connect all those Terms without a parent to the BccRoot
MATCH (t:Term), (top:BccRoot {id:"bccRoot"})
WHERE NOT (t)-[:SUBTERM_OF]->()
MERGE (t)-[:SUBTERM_OF]->(top);